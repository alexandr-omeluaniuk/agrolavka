plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id "io.spring.dependency-management"
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
jacoco {
    toolVersion = "0.8.10"
}
def jacocoExclusions = [
    "ss/entity/**/*.class"
]
jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExclusions)
        }))
    }
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExclusions)
        }))
    }
}
check.dependsOn jacocoTestCoverageVerification

checkstyle {
    maxWarnings = 0
    toolVersion = "10.10.0"
}
checkstyleTest {
    excludes = [
        "**"
    ]
}
tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

tasks.withType(JavaCompile) {
    options.warnings = true
    options.compilerArgs = ["-Xlint:unchecked"]
}
dependencyManagement {
    imports {
        mavenBom (libs.spring.boot.dependencies.get().toString()) {}
    }
}
